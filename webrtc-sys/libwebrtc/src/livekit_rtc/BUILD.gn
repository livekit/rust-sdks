import("../webrtc.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
} else if (is_mac) {
  import("//build/config/mac/rules.gni")
} else if (is_ios) {
  import("//build/config/ios/rules.gni")
} else if (is_linux) {
  import("//build/config/linux/pkg_config.gni")
}

lk_sources = [
  "peer.cc",
  "audio_device.cc",
  "transceiver.cc",
  "utils.cc",
  "data_channel.cc",
  "capi.cc",
  "video_encoder.cc",
  "video_decoder.cc",
  #"android.cc",
]

if (is_mac) {
  lk_sources += [
    "objc_video_factory.mm",
  ]
}

config("livekit_config") {
  include_dirs = [ "." ]
}

rtc_static_library("livekit_rtc") {
  libs = []
  sources = lk_sources
  defines = [
    "USE_LIBYUV",
    "WEBRTC_INCLUDE_INTERNAL_AUDIO_DEVICE",
  ]

  public_configs = [ ":livekit_config" ]

  deps = [
    "../api:create_peerconnection_factory",
    "../api:libjingle_peerconnection_api",
    "../api/audio_codecs:builtin_audio_decoder_factory",
    "../api/audio_codecs:builtin_audio_encoder_factory",
    "../api/crypto:frame_crypto_transformer",
    "../api/video:video_frame",
    "../api/video_codecs:builtin_video_decoder_factory",
    "../api/video_codecs:builtin_video_encoder_factory",
    "../media:rtc_audio_video",
    "../media:rtc_internal_video_codecs",
    "../media:rtc_media",
    "../media:rtc_media_base",
    "../modules/audio_device:audio_device",
    "../modules/audio_processing:api",
    "../modules/audio_processing:audio_processing",
    "../modules/video_capture:video_capture_module",
    "../pc:libjingle_peerconnection",
    "../rtc_base:threading",
    "../sdk:media_constraints",
    "//third_party/abseil-cpp/absl/memory",
    "//third_party/boringssl:boringssl",
    "//third_party/libyuv",
  ]

  if (is_mac) {
    deps += [
      "../sdk:peerconnectionfactory_base_objc",
    ]
  }
}

rtc_test("livekit_rtc_unittests") {
    testonly = true

    sources = [
      "peer.test.cc",
    ]

    deps = [
      ":livekit_rtc",
      "//testing/gtest",
      "//test:test_support",
      "//test:test_main",
    ]
  }
